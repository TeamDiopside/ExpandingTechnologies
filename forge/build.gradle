plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "com.modrinth.minotaur" version "2.+"
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

loom {
    forge {
        mixinConfig("expandingtechnologies-common.mixins.json")
        mixinConfig("expandingtechnologies.mixins.json")
    }
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

repositories {
    maven {
        name 'Progwm16 maven'
        url 'https://dvs1.progwml6.com/files/maven'
    }
    maven {
        name 'tterrag maven'
        url 'https://maven.tterrag.com'
    }
    maven {
        url 'https://www.cursemaven.com'
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.forge_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury-forge:${rootProject.architectury_version}"

    modImplementation("com.simibubi.create:create-${create_minecraft_version}:${create_version}:slim") { transitive = false }
    modImplementation("com.jozufozu.flywheel:flywheel-forge-${flywheel_minecraft_version}:${flywheel_version}")
    modImplementation("com.tterrag.registrate:Registrate:${registrate_version}")

    modCompileOnly("curse.maven:create-steam-n-rails-688231:${steam_n_rails_forge_id}")
    modCompileOnly("curse.maven:create-extended-cogs-739973:${extended_cogs_id}")
    modCompileOnly("curse.maven:create-extended-flywheels-663769:${extended_flywheels_id}")

    //modCompileOnly("mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}")
    //modLocalRuntime("mezz.jei:jei-${minecraft_version}-forge:${jei_version}")

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "expandingtechnologies"
    versionNumber = "${rootProject.mod_version}-forge-1.19.2"
    versionName = "${rootProject.mod_version} Forge 1.19.2"
    versionType = "release"
    uploadFile = remapJar
    gameVersions = ["1.19.2"]
    loaders = ["forge"]
    dependencies {
        required.project "architectury-api"
        required.project "create"
    }
    changelog = rootProject.file("CHANGELOG_LATEST.md").text

    syncBodyFrom = rootProject.file("README.md").text
    tasks.modrinth.dependsOn(tasks.modrinthSyncBody)
}

curseforge {
    project {
        apiKey = System.getenv("CURSEFORGE_TOKEN")
        id = "884893"
        changelog = rootProject.file("CHANGELOG_LATEST.md").text
        changelogType = "markdown"
        releaseType = "release"
        addGameVersion "1.19.2"
        addGameVersion "Forge"

        relations {
            requiredDependency "architectury-api"
            requiredDependency "create"
        }
        mainArtifact(remapJar) {
            displayName = "${rootProject.mod_version} Forge 1.19.2"
        }
        options {
            javaVersionAutoDetect = false
        }
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "fabric.mod.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set("dev-shadow")
}

remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set(null)
}

jar {
    archiveClassifier.set("dev")
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}